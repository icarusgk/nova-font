$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../../support/utils.mjs"

glyph-module


glyph-block Letter-Greek-Lower-Mu : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Latin-U

	define SmallMuConfig : object
		tailless          { 'u.toothedSerifless'          'u.toothedSerifed'          }
		tailed            { 'u.tailedSerifless'           'u.tailedSerifed'           }
		toothlessCorner   { 'u.toothlessCornerSerifless'  'u.toothlessCornerSerifed'  }
		toothlessRounded  { 'u.toothlessRoundedSerifless' 'u.toothlessRoundedSerifed' }

	foreach { suffix { ptSerifless ptSerifed } } [Object.entries SmallMuConfig] : do
		create-glyph "grek/mu.\(suffix)" : glyph-proc
			include : MarkSet.p
			include : add-glyph-dependency ptSerifless
			include : add-glyph-dependency ptSerifed

			include : refer-glyph [if SLAB ptSerifed ptSerifless]
			include : dispiro
				widths.rhs
				flat SB Descender [heading Upward]
				curl SB (Descender / 2) [heading Upward]
				straight.up.end SB SmallArchDepthB [widths.heading 0 [AdviceStroke 4] Upward]
			if SLAB : begin
				include : HSerif.lt SB XH SideJut

	select-variant 'grek/mu' 0x3BC
	select-variant 'micro'   0xB5  (shapeFrom -- 'grek/mu')
