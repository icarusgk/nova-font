$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback SuffixCfg] from"../../../support/utils.mjs"
import [Dotless CvDecompose MathSansSerif] from"../../../support/gr.mjs"

glyph-module

glyph-block Letter-Latin-Lower-R : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared : CreateAccentedComposition
	glyph-block-import Letter-Shared-Shapes : DToothlessRise DMBlend PalatalHook RetroflexHook
	glyph-block-import Letter-Blackboard : BBS BBD BBBarRight

	local dfN : DivFrame 1
	local dfR : DivFrame para.diversityF
	local rStraight             0
	local rSerifed              1
	local rNarrow               2
	local rNarrowSerifed        3
	local rEarless              4
	local rCornerHooked         5
	local rCornerHookedSerifed  6

	define [RDim df mode _strokeBar _stroke] : begin
		local strokeBar : fallback _strokeBar Stroke
		local stroke : fallback _stroke Stroke
		local strokeA : mix strokeBar stroke 0.5
		local { rBalanceMultiplier rHookMultiplier rHookSwMultiplier rSerifLeftExtender hookSuperness } : match mode
			[Just rStraight]            {  1                     1        0        0                                       2.35 }
			[Just rSerifed]             { (4 / 3)               (2 / 3)  (1 / 4)  (1 / 3)                                  2.35 }
			[Just rNarrow]              { (2 * (df.div - 0.5))   1        0        0                                       2.35 }
			[Just rNarrowSerifed]       { (5 / 8 * df.div)      (3 / 4)   0       (1 / 2 * [Math.max 0 : mix 1 df.div 4])  2.35 }
			[Just rCornerHooked]        { (2 * (df.div - 0.5))   1        0        0                                       2.35 }
			[Just rCornerHookedSerifed] { (4 / 3)               (2 / 3)  (1 / 4)  (1 / 3)                                  2.35 }
			[Just rEarless]             {  1                     1        0        0                                       2.35 }

		local xBar : match mode
			[Just rNarrowSerifed] : df.middle + 0.5 * HVContrast * strokeBar - RBalance * rBalanceMultiplier
			__                    : SB + RBalance * rBalanceMultiplier + strokeBar * HVContrast
		local rSerifX : xBar - strokeBar / 2 * HVContrast
		local rSerifLeftJut  : SideJut + RBalance * (0.3 + rSerifLeftExtender)
		local rSerifRightJut : rSerifLeftJut * 1.20
		local [rBottomSerif y] : glyph-proc
			include : tagged 'serifLB' : union
				HSerif.lb  rSerifX y (rSerifLeftJut + (strokeBar / 2) * HVContrast)
				HSerif.rb rSerifX y (rSerifRightJut + (strokeBar / 2) * HVContrast)
			local xAtt : rSerifX + rSerifRightJut + (strokeBar / 2) * HVContrast
			set-base-anchor 'palatalHookAttach' xAtt 0
			set-base-anchor 'palatalHookPos'    xAtt 0

		local [rTopSerif y] : tagged 'serifLT'
			HSerif.lt rSerifX y (rSerifLeftJut + (strokeBar / 2) * HVContrast)
		local fine ShoulderFine
		local rHookX : df.rightSB + RBalance2 * rBalanceMultiplier - (OX - O)
		local xArchMiddle : match mode
			[Just rStraight]            : mix (xBar - fine) rHookX (0.54 + 2 * TanSlope * strokeBar / Width)
			[Just rSerifed]             : mix (xBar - fine) rHookX (0.59 + 2 * TanSlope * strokeBar / Width)
			[Just rNarrow]              : mix df.width rHookX : Math.max 1.01 (5 / 4 * [mix 1 dfR.div 2])
			[Just rNarrowSerifed]       : Math.min ([mix df.width rHookX df.div] - 0.1) : xBar + RHook * 1.25 * df.div
			[Just rCornerHooked]        : rHookX - strokeBar / 2 * HVContrast
			[Just rCornerHookedSerifed] : rHookX - strokeBar / 2 * HVContrast
			[Just rEarless]             : mix (xBar - strokeBar * HVContrast) rHookX 0.5
		local mixpin : match mode
			([Just rSerifed] || [Just rCornerHooked] || [Just rCornerHookedSerifed]) : begin
				0.65 + 0.25 * strokeA / Width + 4 * TanSlope * strokeA / Width
			__ : 0.65 + 4 * TanSlope * strokeA / Width
		local rmiddlein : [mix xBar (rHookX - strokeA * HVContrast * 1.05) mixpin] - CorrectionOMidS
		local skew : Math.max 0 : (xArchMiddle - rmiddlein) / stroke - TanSlope
		local rHookY : RHook * rHookMultiplier + stroke * rHookSwMultiplier
		local rHookXN : match mode
			[Just rNarrowSerifed] : mix df.width rHookX df.div
			[Just rNarrow]        : xArchMiddle + 0.1
			__                      rHookX
		local [setMarks doTopSerif] : glyph-proc
			set-base-anchor 'above' [mix [mix df.leftSB (xBar - Stroke * HVContrast) : if doTopSerif 0.5 1] df.rightSB 0.5] XH
			set-base-anchor 'overlay' (xBar - Stroke * 0.25) (XH * 0.5)
			set-base-anchor 'palatalHookAttach' xBar 0
			set-base-anchor 'palatalHookPos' (xBar + [PalatalHook.adviceGap Stroke]) 0

		return : object xBar rBottomSerif rTopSerif fine xArchMiddle skew rHookX rHookXN rHookY hookSuperness setMarks

	define [StandardShape df md doTopSerif doBottomSerif] : glyph-proc
		define [object xBar rBottomSerif rTopSerif fine xArchMiddle skew rHookX rHookY hookSuperness setMarks] : RDim df md
		include : setMarks doTopSerif
		include : dispiro
			widths.lhs
			g4.up.start rHookX (XH - rHookY - Stroke * 0.5) [heading Upward]
			arcvh 32 hookSuperness
			g4.left.mid (xArchMiddle - CorrectionOMidS * [linreg 72 0.75 108 1 Stroke]) (XH - O) [widths.heading Stroke 0 {.y (-1) .x (-skew)}]
			archv 32
			straight.down.end (xBar - fine * HVContrast) (XH * 0.53 + (SmallArchDepth - SmallArchDepthA)) [widths.heading fine 0 Downward]
		include : VBar.r xBar 0 XH
		if doBottomSerif : include : rBottomSerif 0
		if doTopSerif : include : rTopSerif XH

	define [CompactShape    df md ts bs] : CompactShapeImpl df md false ts bs
	define [CornerHookShape df md ts bs] : CompactShapeImpl df md true  ts bs
	define [CompactShapeImpl df md doHookSerif doTopSerif doBottomSerif] : glyph-proc
		define [object xBar rBottomSerif rTopSerif fine xArchMiddle rHookXN setMarks] : RDim df md
		include : setMarks doTopSerif
		define xCor : if doHookSerif 0 : CorrectionOMidS * [linreg 72 0.75 108 1 Stroke]

		define arcTopShift : match md
			[Just rNarrow] : O * [clamp 0 1 : StrokeWidthBlend 0 16]
			__               0
		define arcTopWidth : match md
			[Just rNarrow] : Stroke - O * [clamp 0 1 : StrokeWidthBlend 0 16]
			__               Stroke

		define [ArcKnots] : list
			flat (rHookXN - xCor) (XH - arcTopShift) [if doHookSerif [heading Leftward] null]
			curl (xArchMiddle - xCor) (XH - arcTopShift) [if doHookSerif [heading Leftward] null]
			archv
			straight.down.end (xBar - fine * HVContrast) (XH * 0.53 + (SmallArchDepth - SmallArchDepthA)) [widths.heading fine 0 Downward]
		include : dispiro [widths.lhs arcTopWidth] [ArcKnots]
		include : VBar.r xBar 0 XH
		if doHookSerif : include : intersection
			VSerif.dr (rHookXN - xCor) XH VJut
			spiro-outline
				ArcKnots
				corner (xBar - fine * HVContrast) 0
				corner (Width * 16) 0
				corner (Width * 16) XH
		if doBottomSerif : include : rBottomSerif 0
		if doTopSerif : include : rTopSerif XH

	define [EarlessCornerShape df md doTopSerif doBottomSerif] : glyph-proc
		define [object xBar xArchMiddle rHookX rHookY hookSuperness rBottomSerif setMarks] : RDim df md
		include : setMarks doTopSerif
		include : dispiro
			widths.lhs
			g4.up.start rHookX (XH - rHookY - Stroke * 0.5) [heading Upward]
			arcvh nothing hookSuperness
			g4.left.mid (xArchMiddle - CorrectionOMidS) (XH - O) [heading Leftward]
			g4 (xBar - Stroke * HVContrast) (XH - DToothlessRise)
		include : VBar.r xBar 0 (XH - DToothlessRise)
		if doBottomSerif : include : rBottomSerif 0

	define [EarlessRoundedShape df md doTopSerif doBottomSerif] : glyph-proc
		define [object xBar xArchMiddle rHookX rHookY hookSuperness rBottomSerif setMarks] : RDim df md
		include : setMarks doTopSerif
		local hx : Math.max rHookX (xBar + 1.25 * Stroke)
		include : dispiro
			widths.lhs
			g4   hx (XH - rHookY)
			hookstart (XH - O)
			flat (xBar - Stroke * HVContrast) (XH - SmallArchDepthA)
			curl (xBar - Stroke * HVContrast) 0 [heading Downward]
		if doBottomSerif : include : rBottomSerif 0

	define [FlapHooklessShape df md doTopSerif doBottomSerif] : glyph-proc
		define [object xBar rBottomSerif xArchMiddle setMarks] : RDim df md
		set-base-anchor 'overlay' (xBar - 0.5 * Stroke * HVContrast) (XH / 2)
		include : setMarks doTopSerif
		include : dispiro
			widths.lhs
			g4.left.start (xArchMiddle - CorrectionOMidS * [linreg 72 0.75 108 1 Stroke]) (XH - O)
			archv
			flat (xBar - Stroke * HVContrast) (XH - SmallArchDepthA)
			curl (xBar - Stroke * HVContrast) 0 [heading Downward]
		if doBottomSerif : include : rBottomSerif 0

	define SmallRConfig : SuffixCfg.weave
		# Body
		object
			''             { StandardShape       dfN rStraight     }
			earlessCorner  { EarlessCornerShape  dfN rEarless      }
			earlessRounded { EarlessRoundedShape dfN rEarless      }
			hookless       { CompactShape        dfN rNarrow       }
			cornerHooked   { CornerHookShape     dfN rCornerHooked }
			compact        { CompactShape        dfR rNarrow       }
			hooklessFlap   { FlapHooklessShape   dfN rNarrow       }
			compactFlap    { FlapHooklessShape   dfR rNarrow       }

		# Serifs
		object
			serifless      { 0 0 }
			topSerifed     { 1 0 }
			baseSerifed    { 0 1 }
			serifed        { 1 1 }

	foreach { suffix { {F df mode} {doTS doBS} } } [pairs-of SmallRConfig] : do
		create-glyph "r.\(suffix)" : glyph-proc
			set-width df.width
			include : df.markSet.e
			include : F df mode doTS doBS

		create-glyph "rLongLeg.\(suffix)" : glyph-proc
			set-width df.width
			include : df.markSet.p
			define [object xBar rBottomSerif] : RDim df mode
			include : F df mode doTS 0
			eject-contour 'serifLB'
			include : VBar.r xBar Descender 0
			if doBS : include : rBottomSerif Descender

		create-glyph "rCapLongLeg.\(suffix)" : glyph-proc
			set-width df.width
			include : df.markSet.capDesc
			define [object xBar rBottomSerif] : RDim df mode
			include : WithTransform [ApparentTranslate 0 (CAP - XH)] : F df mode doTS 0
			eject-contour 'serifLB'
			include : VBar.r xBar Descender (CAP - XH)
			if doBS : include : rBottomSerif Descender

		create-glyph "fInsular.\(suffix)" : glyph-proc
			include [refer-glyph "rLongLeg.\(suffix)"] AS_BASE ALSO_METRICS
			define [object xBar rHookX] : RDim df mode
			include : HBar.b xBar (rHookX - [Math.max (0.15 * (df.rightSB - df.leftSB)) (0.25 * HVContrast * Stroke)]) 0

		create-glyph "FInsular.\(suffix)" : glyph-proc
			include [refer-glyph "rCapLongLeg.\(suffix)"] AS_BASE ALSO_METRICS
			define [object xBar rHookX] : RDim df mode
			include : HBar.b xBar (rHookX - [Math.max (0.15 * (df.rightSB - df.leftSB)) (0.25 * HVContrast * Stroke)]) 0

		create-glyph "rPalatalHook.\(suffix)" : glyph-proc
			include [refer-glyph "r.\(suffix)"] AS_BASE ALSO_METRICS
			local attach : currentGlyph.gizmo.unapply currentGlyph.baseAnchors.palatalHookAttach
			local pos : currentGlyph.gizmo.unapply currentGlyph.baseAnchors.palatalHookPos
			include : PalatalHook.r (x -- pos.x) (y -- pos.y) (xLink -- attach.x)

		create-glyph "rRTail.\(suffix)" : glyph-proc
			set-width df.width
			include : df.markSet.e
			include : F df mode doTS 0
			eject-contour 'serifLB'

			define [object xBar] : RDim df mode
			include : RetroflexHook.rExt xBar 0

		create-glyph "rTurnRTail.\(suffix)" : glyph-proc
			set-width df.width
			include : df.markSet.e
			include : F df mode 0 doBS
			eject-contour 'serifLT'
			include : FlipAround df.middle (XH / 2)

			define [object xBar] : RDim df mode
			include : RetroflexHook.lExt (df.rightSB - xBar + df.leftSB) 0

		create-glyph "rTurnLongLetRTail.\(suffix)" : glyph-proc
			include [refer-glyph "rLongLeg.\(suffix)"] AS_BASE ALSO_METRICS
			eject-contour 'serifLT'
			include : FlipAround df.middle (XH / 2)
			include : df.markSet.b

			define [object xBar] : RDim df mode
			include : RetroflexHook.lExt (df.rightSB - xBar + df.leftSB) 0

	select-variant 'r' 'r'
	link-reduced-variant 'r/sansSerif' 'r' MathSansSerif

	define [TurnRMarks k] : TurnMarks k
		function [df] : return {.baseAnchors {.bottomRight {.x (df.rightSB - RBalance) .y 0}}}
	turned 'turnr' 0x279 'r' HalfAdvance (XH / 2) [TurnRMarks 'e']

	CreateAccentedComposition 'rTildeOver' 0x1D72 'r' 'tildeOver'
	turned 'turnrTildeOver' 0xAB68 'rTildeOver' HalfAdvance (XH / 2) [TurnRMarks 'e']

	select-variant 'rLongLeg' 0x27C (follow -- 'r')
	select-variant 'SInsular' 0xA784 (shapeFrom -- 'rCapLongLeg') (follow -- 'rRTail')
	select-variant 'sInsular' 0xA785 (shapeFrom -- 'rLongLeg') (follow -- 'rRTail')
	select-variant 'FInsular' 0xA77B (follow -- 'rRTail')
	select-variant 'fInsular' 0xA77C (follow -- 'rRTail')
	turned 'turnrLongLeg' 0x27A 'rLongLeg' HalfAdvance (XH / 2) [TurnRMarks 'b']

	select-variant 'rRTail' 0x27D
	turned 'smallLetterTurnedRWithTail' 0x2C79 'rRTail' HalfAdvance (XH / 2) : glyph-proc
		local df : DivFrame (currentGlyph.advanceWidth / Width)
		include : df.markSet.b

	select-variant 'rTurnRTail' 0x27B
	select-variant 'rTurnLongLetRTail' 0x1DF08 (follow -- 'rTurnRTail')
	select-variant 'rPalatalHook' 0x1D89 (follow -- 'r')

	define [BBRShape df md doTopSerif doBottomSerif] : glyph-proc
		define [object xBar fine xArchMiddle skew rHookX rHookY hookSuperness] : RDim df md BBD BBS
		include : dispiro
			widths.lhs BBS
			g4.up.start rHookX (XH - rHookY - BBS * 0.5) [heading Upward]
			arcvh nothing hookSuperness
			g4.left.mid (xArchMiddle - CorrectionOMidS * [linreg 72 0.75 108 1 BBS]) (XH - O) [widths.heading BBS 0 {.y (-1) .x (-skew)}]
			archv
			straight.down.end (xBar - fine * HVContrast) (XH * 0.53 + (SmallArchDepth - SmallArchDepthA)) [widths.heading fine 0 Downward]
		include : BBBarRight xBar 0 XH
		set-base-anchor 'overlay' (xBar - HVContrast * (BBD + BBS * 2) * 0.25) (XH * 0.5)

	create-glyph 'mathbb/r' 0x1D563 : glyph-proc
		include : dfR.markSet.e
		include : BBRShape dfN rStraight 0 0

	select-variant 'rFlap' 0x27E (shapeFrom -- 'r')
	CreateAccentedComposition 'rFlapTildeOver' 0x1D73 'rFlap' 'tildeOver'
