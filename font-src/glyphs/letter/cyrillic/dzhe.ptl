$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../../support/utils.mjs"

glyph-module

glyph-block Letter-Cyrillic-Dzhe : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : SerifFrame

	define [CyrDzheShape top] : glyph-proc
		include : VBar.l SB 0 top
		include : HBar.b SB RightSB 0
		include : VBar.r RightSB 0 top
		include : VBar.m Middle Descender Stroke
		if SLAB : let [sf : SerifFrame.fromDf [DivFrame 1] top 0] : begin
			include : composite-proc sf.lt.full sf.rt.full sf.lb.outer sf.rb.outer

	create-glyph 'cyrl/Dzhe' 0x40F : glyph-proc
		include : MarkSet.capDesc
		include : CyrDzheShape CAP

	create-glyph 'cyrl/dzhe.upright' : glyph-proc
		include : MarkSet.p
		include : CyrDzheShape XH

	define ItalicVariants {
		'toothedSerifless'
		'toothedMotionSerifed'
		'toothedBottomRightSerifed'
		'toothedSerifed'
		'tailedSerifless'
		'tailedMotionSerifed'
		'tailedSerifed'
	}
	foreach suffix [items-of ItalicVariants] : do
		create-glyph "cyrl/dzhe.italic.\(suffix)" : glyph-proc
			include [refer-glyph "u.\(suffix)"] AS_BASE ALSO_METRICS
			include [refer-glyph 'descenderBarBelow']

	select-variant 'cyrl/dzhe.italic'
