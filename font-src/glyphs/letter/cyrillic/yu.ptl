$$include '../../../meta/macros.ptl'

import [mix barMixL linreg clamp fallback] from"../../../support/utils.mjs"

glyph-module

glyph-block Letter-Cyrillic-Yu : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Cyrillic-Iotified-A : Iotified

	define SLAB-ALL        1
	define SLAB-LOWER      2
	define SLAB-BULGARIAN  3

	define SLAB-NONE		0
	define SLAB-FULL		1
	define SLAB-OUTWARD		2

	define [CyrYuShape df slabType top xtop ada adb] : glyph-proc
		local xm : barMixL df.leftSB df.rightSB (df.mvs * HVContrast) [StrokeWidthBlend 0.4 0.45]
		include : OShape top 0 xm df.rightSB df.mvs (ada * 0.7 * df.div) (adb * 0.7 * df.div)

		include : if (slabType === SLAB-BULGARIAN)
			Iotified.ascender df xtop xm (top / 2)
			Iotified.full df xtop xm (top / 2) (fCapital -- (slabType === SLAB-ALL))

	define [CyrRevYuShape df slabType top xtop ada adb] : glyph-proc
		local xm : barMixL df.leftSB df.rightSB (df.mvs * HVContrast) [StrokeWidthBlend 0.4 0.45]
		local revXm : df.leftSB + df.rightSB - xm
		include : OShape top 0 df.leftSB revXm df.mvs (ada * 0.7 * df.div) (adb * 0.7 * df.div)

		local useItalicShape : slabType !== SLAB-ALL && para.isItalic
		local slabTop : if useItalicShape SLAB-NONE SLAB-FULL
		local slabBottom : if useItalicShape SLAB-OUTWARD SLAB-FULL

		include : Iotified.RevShape df xtop
			hBarLeft   -- revXm
			hBarY      -- (top / 2)
			slabTop    -- slabTop
			slabBottom -- slabBottom

	create-glyph 'cyrl/Yu' 0x42E : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.capital
		include : CyrYuShape df SLAB-ALL CAP CAP ArchDepthA ArchDepthB

	create-glyph 'cyrl/yu' 0x44E : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.e
		include : CyrYuShape df SLAB-LOWER XH XH SmallArchDepthA SmallArchDepthB

	create-glyph 'cyrl/yu.BGR' : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.b
		include : CyrYuShape df SLAB-BULGARIAN XH Ascender SmallArchDepthA SmallArchDepthB

	create-glyph 'cyrl/YuRev' 0xA654 : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.capital
		include : CyrRevYuShape df SLAB-ALL CAP CAP ArchDepthA ArchDepthB

	create-glyph 'cyrl/yuRev' 0xA655 : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.e
		include : CyrRevYuShape df SLAB-LOWER XH XH SmallArchDepthA SmallArchDepthB
