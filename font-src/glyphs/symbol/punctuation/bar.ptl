### Punctuation symbols
$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../../support/utils.mjs"
import [Joining] from"../../../support/gr.mjs"
import [DesignParameters] from"../../../meta/aesthetics.mjs"

glyph-module

glyph-block Symbol-Punctuation-Bar : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	define [BarShape x gap] : glyph-proc
		set-base-anchor 'above' x ParenTop
		set-base-anchor 'below' x ParenBot
		include : VBar.m x (ParenBot + gap) (ParenTop - gap)

	create-glyph 'bar.upright' : BarShape Middle 0
	create-glyph 'bar.slanted.naturalSlope' : BarShape Middle 0
	create-glyph 'bar.slanted.forceUpright' : glyph-proc
		include : ForceUpright
		include : BarShape Middle 0

	create-glyph 'ligBarInsideBracketLeft' : glyph-proc
		Joining.set currentGlyph Joining.Classes.Left
		include : BarShape
			[mix SB RightSB DesignParameters.bracketOutside] + HalfStroke * HVContrast
			Stroke + [Math.max [AdviceStroke 5] (XH / 12)]

	create-glyph 'ligBarInsideBracketRight' : glyph-proc
		Joining.set currentGlyph Joining.Classes.Right
		include : BarShape
			[mix RightSB SB DesignParameters.bracketOutside] - HalfStroke * HVContrast
			Stroke + [Math.max [AdviceStroke 5] (XH / 12)]

	define [DoubleBarShape] : glyph-proc
		set-base-anchor 'above' Middle ParenTop
		set-base-anchor 'below' Middle ParenBot
		local sw : AdviceStroke 3.5
		include : VBar.m (Middle - Width * 0.175) ParenBot ParenTop sw
		include : VBar.m (Middle + Width * 0.175) ParenBot ParenTop sw

	create-glyph 'doubleBar.upright'              : DoubleBarShape
	create-glyph 'doubleBar.slanted.naturalSlope' : DoubleBarShape
	create-glyph 'doubleBar.slanted.forceUpright' : glyph-proc
		include : ForceUpright
		include : DoubleBarShape

	define [TripleBarShape] : glyph-proc
		set-base-anchor 'above' Middle ParenTop
		set-base-anchor 'below' Middle ParenBot
		local sw : AdviceStroke 4.25
		include : VBar.m (Middle - Width * 0.2625) ParenBot ParenTop sw
		include : VBar.m  Middle                   ParenBot ParenTop sw
		include : VBar.m (Middle + Width * 0.2625) ParenBot ParenTop sw

	create-glyph 'tripleBar.upright'              : TripleBarShape
	create-glyph 'tripleBar.slanted.naturalSlope' : TripleBarShape
	create-glyph 'tripleBar.slanted.forceUpright' : glyph-proc
		include : ForceUpright
		include : TripleBarShape

	define [BrokenBarShape] : glyph-proc
		local breakDist : Math.max Stroke (CAP / 8)
		include : VBar.m Middle (SymbolMid + breakDist / 2) ParenTop
		include : VBar.m Middle ParenBot (SymbolMid - breakDist / 2)

	create-glyph 'brokenBar.upright'              : BrokenBarShape
	create-glyph 'brokenBar.slanted.naturalSlope' : BrokenBarShape
	create-glyph 'brokenBar.slanted.forceUpright' : glyph-proc
		include : ForceUpright
		include : BrokenBarShape

	create-glyph 'palatoalveolarclick' 0x1C2 : glyph-proc
		include [refer-glyph 'bar.upright'] AS_BASE
		include : HBar.b SB RightSB (SymbolMid + XH * 0.1) OperatorStroke
		include : HBar.t SB RightSB (SymbolMid - XH * 0.1) OperatorStroke

	select-variant 'bar.slanted'
	orthographic-slanted 'bar' '|'
	select-variant 'doubleBar.slanted' (follow -- 'bar.slanted')
	orthographic-slanted 'doubleBar' 0x2016
	select-variant 'tripleBar.slanted' (follow -- 'bar.slanted')
	orthographic-slanted 'tripleBar' 0x2AFC
	select-variant 'brokenBar.slanted' (follow -- 'bar.slanted')
	orthographic-slanted 'brokenBar' 0xA6
	alias 'dentalclick' 0x1C0 'bar.upright'
	alias 'alveolarlateralclick' 0x1C1 'doubleBar.upright'
	alias 'retroflexlateralclick' 0x2980 'tripleBar.upright'
